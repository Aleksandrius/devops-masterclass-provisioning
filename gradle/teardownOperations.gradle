import groovy.json.JsonSlurper

task disableTerminationProtection (type: Exec, group: 'Infrastructure shutdown') {
  commandLine "aws", "ec2", "modify-instance-attribute", "--instance-id", project.getServerInstanceId(), "--no-disable-api-termination"
}

task terminateAllServers(dependsOn: [describeInstances, disableTerminationProtection], group: 'Infrastructure shutdown') {
  doLast {
    def instances = new JsonSlurper().parse(file('instances.json'))
    instances.Reservations.each { reservation ->
      reservation.Instances.each { instance ->
        if (instance.State.Name == 'running') {
          exec {
            commandLine "aws", "ec2", "terminate-instances", "--instance-ids", instance.InstanceId
          }
        }
      }
    }
  }
}

task deleteAllKeypairs(dependsOn: [describeKeypairs], group: 'Infrastructure shutdown') {
  doLast {
    def instances = new JsonSlurper().parse(file('keypairs.json'))
    instances.KeyPairs.each { keyPair ->
      exec {
        commandLine "aws", "ec2", "delete-key-pair", "--key-name", keyPair.KeyName
      }
    }
  }
}
